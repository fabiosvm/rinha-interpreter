
chunk           ::= expr EOF

expr            ::= and_expr ( '||' and_expr )*

and_expr        ::= eq_expr ( '&&' eq_expr )*

eq_expr         ::= comp_expr ( ( '==' | '!=' ) comp_expr )*

comp_expr       ::= add_expr ( ( '<' | '<=' | '>' | '>=' ) add_expr )*

add_expr        ::= mul_expr ( ( '+' | '-' ) mul_expr )*

mul_expr        ::= prim_expr ( ( '*' | '/' | '%' ) prim_expr )*

prim_expr       ::= bool
                  | INT
                  | STR
                  | tuple
                  | block
                  | 'fn' '(' ( IDENT ( ',' IDENT )* )? ')' '=>' expr
                  | 'let' IDENT '=' expr ';' expr
                  | 'if' '(' expr ')' block 'else' block
                  | ( 'print' | 'first' | 'second' ) '(' expr ')'
                  | IDENT call?
                  | '(' expr ')'

bool            ::= 'false' | 'true'

tuple           ::= '(' expr ',' expr ')'

block           ::= '{' expr '}'

call            ::= '(' ( expr ( ',' expr )* )? ')'
